plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "jacoco"
    id "com.gradle.plugin-publish" version "0.10.1"
    id 'org.ajoberstar.reckon' version "0.9.0"
    id "org.sonarqube" version "2.6.2"
}
group 'be.vbgn.gradle'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integrationTest/java')
        }

        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom(testCompile)
    integrationTestRuntime.extendsFrom(testRuntime)
}

task integrationTest(type: Test, group: "verification") {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    dependsOn(publishToMavenLocal)
    doFirst {
        systemProperty "pluginVersion", project.version
    }
}

integrationTest.mustRunAfter(test)
check.dependsOn(integrationTest)
tasks.sonarqube.dependsOn(integrationTest)

sonarqube {
    properties {
        properties["sonar.tests"] += sourceSets.integrationTest.allSource.srcDirs
    }
}

javadoc {
    exclude '**/internal/**'
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile gradleTestKit()
    testCompile group: 'org.mockito', name: 'mockito-inline', version: '3.1.0'
    integrationTestCompile group: 'commons-io', name: 'commons-io', version: '2.6'
}

gradlePlugin {
    plugins {
        pluginUpdates {
            id = "be.vbgn.plugin-updates"
            implementationClass = "be.vbgn.gradle.pluginupdates.PluginUpdatesPlugin"
        }
        config {
            id = "be.vbgn.plugin-updates.config"
            implementationClass = "be.vbgn.gradle.pluginupdates.ConfigurationPlugin"
        }
    }
}

pluginBundle {
    vcsUrl = "https://github.com/vierbergenlars/plugin-updates-gradle-plugin"
    website = vcsUrl
    description = "A gradle plugin that automatically notifies about plugins that can be updated."
    tags = ["plugin", "update", "gradle-updates"]
    plugins {
        pluginUpdates {
            displayName = "Plugin updates plugin"
        }
        config {
            displayName = "Plugin updates plugin configuration"
        }
    }
}

reckon {
    scopeFromProp()
    snapshotFromProp()
}

reckonTagCreate.dependsOn(check)
