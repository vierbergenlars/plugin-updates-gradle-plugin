plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'jacoco'
    id 'com.gradle.plugin-publish' version '0.17.0'
    id 'org.ajoberstar.reckon' version '0.13.0'
    id 'org.sonarqube' version '3.3'
    id 'be.vbgn.ci-detect' version '0.5.0'
    id 'be.vbgn.dev-conventions.opinion' version '0.5.0'
    id 'nebula.integtest' version '9.1.6'
}
group 'be.vbgn.gradle'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

test {
    maxParallelForks = 8
}

integrationTest {
    maxParallelForks = 8
    dependsOn(publishToMavenLocal)
    doFirst {
        systemProperty "pluginVersion", project.version
    }
    shouldRunAfter(test)
}

sonarqube {
    properties {
        properties["sonar.tests"] += sourceSets.integTest.allSource.srcDirs
    }
}

javadoc {
    exclude '**/internal/**'
}

dependencies {
    testCompile 'junit:junit:4.13.2'
    testCompile 'org.mockito:mockito-inline:4.0.0'
    integTestCompile 'commons-io:commons-io:2.11.0'
}

gradlePlugin {
    plugins {
        pluginUpdates {
            id = "be.vbgn.plugin-updates"
            implementationClass = "be.vbgn.gradle.pluginupdates.PluginUpdatesPlugin"
        }
        config {
            id = "be.vbgn.plugin-updates.config"
            implementationClass = "be.vbgn.gradle.pluginupdates.ConfigurationPlugin"
        }
    }
}

pluginBundle {
    vcsUrl = "https://github.com/vierbergenlars/plugin-updates-gradle-plugin"
    website = vcsUrl
    description = "A gradle plugin that automatically notifies about plugins that can be updated."
    tags = ["plugin", "update", "gradle-updates"]
    plugins {
        pluginUpdates {
            displayName = "Plugin updates plugin"
        }
        config {
            displayName = "Plugin updates plugin configuration"
        }
    }
}
